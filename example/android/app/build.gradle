plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
    id "com.huawei.agconnect"
    id "com.google.gms.google-services"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

// Helper function to get BOM version from moengage_flutter module or root project
def getMoEngageBomVersion(propertyName, defaultValue) {
    // Try to find moengage_flutter_android project (the Android implementation)
    def moengageFlutterProjectAndroid = findProject(':moengage_flutter_android')
    def moengageFlutterProject = moengageFlutterProjectAndroid ?: findProject(':moengage_flutter')
    
    if (moengageFlutterProject != null) {
        if (moengageFlutterProject.ext.has(propertyName)) {
            return moengageFlutterProject.ext[propertyName]
        }
        // Try PascalCase version (MoEngageNativeBomVersion)
        def pascalCaseProperty = 'Mo' + propertyName.substring(2, 3).toUpperCase() + propertyName.substring(3)
        if (moengageFlutterProject.ext.has(pascalCaseProperty)) {
            return moengageFlutterProject.ext[pascalCaseProperty]
        }
        def value = moengageFlutterProject.ext.properties.get(pascalCaseProperty)
        if (value != null) {
            return value
        }
    }
    
    // Fallback: try root project ext
    if (rootProject.ext.has('MoEngage') && rootProject.ext.MoEngage.hasProperty(propertyName)) {
        return rootProject.ext.MoEngage[propertyName]
    }
    
    // Final fallback
    return project.findProperty(propertyName) ?: defaultValue
}

ext {
  moengageNativeBomVersion = getMoEngageBomVersion('moengageNativeBomVersion', '0.0.0')
}

android {
    namespace = "com.moengage.sampleapp"
    compileSdk = flutter.compileSdkVersion
    // NDK version removed - using default NDK to avoid corrupted NDK installation issues

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId = "com.moengage.sampleapp"
        minSdk = 23
        targetSdk = 35
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField("String", "MOENGAGE_WORKSPACE_ID", "\"${localProperties.getProperty("moengageWorkspaceId")}\"")
    }

  signingConfigs {
    Properties properties = new Properties()
    properties.load(file('keystore.properties').newDataInputStream())
    release {
      storeFile file(properties.getProperty("storeFilePath"))
      storePassword properties.getProperty("storePassword")
      keyAlias properties.getProperty("keyAlias")
      keyPassword properties.getProperty("keyPassword")
    }
    debug {
      storeFile file(properties.getProperty("storeFilePath"))
      storePassword properties.getProperty("storePassword")
      keyAlias properties.getProperty("keyAlias")
      keyPassword properties.getProperty("keyPassword")
    }
  }

    buildTypes {
        release {
            signingConfig = signingConfigs.release
            proguardFiles(getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro')
        }
        debug {
            signingConfig = signingConfigs.debug
        }
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    buildFeatures {
        buildConfig = true
    }
}

flutter {
    source = "../.."
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar', "*.aar"])

  /**
   * MoEngage dependencies
   */
  implementation(platform("com.moengage:android-bom:$moengageNativeBomVersion"))
  implementation("com.moengage:hms-pushkit")
  implementation("com.moengage:rich-notification")

  /**
   * MoEngage required external dependencies (based on the modules you use)
   */
  implementation(moengage.firebaseMessaging)
  implementation(moengage.androidXLifecycle)
  implementation(moengage.gmsPlayAdIdentifier)
  implementation(moengage.hmsPushKit)
  implementation(moengage.gmsPlayLocation)
  implementation(moengage.glide)

  implementation(appLibs.kotlin)

  testImplementation(appLibs.junit)

  androidTestImplementation(appLibs.androidJUnit)
  androidTestImplementation(appLibs.expresso)
}
