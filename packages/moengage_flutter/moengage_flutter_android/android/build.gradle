group 'com.moengage.flutter'
version '1.0'

buildscript {
    ext.kotlin_version = '1.9.23'
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.7.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // classpath "org.jlleitschuh.gradle:ktlint-gradle:12.1.1"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

// Read BOM versions from gradle.properties
def moengageNativeBomVersion = project.findProperty('MoEngageNativeBomVersion') ?: "0.0.0"
def moengagePluginBaseBomVersion = project.findProperty('MoEngagePluginBaseBomVersion') ?: "0.0.0"

// Log BOM versions for verification
println("=========================================")
println("Flutter Core Module - BOM Versions:")
println("  MoEngageNativeBomVersion: $moengageNativeBomVersion")
println("  MoEngagePluginBaseBomVersion: $moengagePluginBaseBomVersion")
println("  Source: gradle.properties")
println("=========================================")

// Expose versions as project properties so other modules can access them
ext {
    moengageNativeBomVersion = moengageNativeBomVersion
    moengagePluginBaseBomVersion = moengagePluginBaseBomVersion
}

// Store in project properties for access by other modules
project.ext.MoEngageNativeBomVersion = moengageNativeBomVersion
project.ext.MoEngagePluginBaseBomVersion = moengagePluginBaseBomVersion

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
// apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdk 35
    namespace "com.moengage.flutter"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 23
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        buildConfig true
    }
}
dependencies {
    implementation(platform("com.moengage:android-bom:$moengageNativeBomVersion"))
    api("com.moengage:moe-android-sdk")
    api("com.moengage:inapp")

    implementation(platform("com.moengage:plugin-base-bom:$moengagePluginBaseBomVersion"))
    api("com.moengage:plugin-base")

    compileOnly("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version")
}
apply from: file("./user-agent.gradle")
